apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.dokka-android'
apply plugin: 'maven-publish'

android {
    compileSdkVersion ANDROID_COMPILE_SDK_VERSION
    buildToolsVersion ANDROID_BUILD_TOOLS_VERSION
    defaultConfig {
        minSdkVersion ANDROID_MIN_SDK_VERSION
        targetSdkVersion ANDROID_TARGET_SDK_VERSION
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {}
        release {
            minifyEnabled false
        }
    }
}

dependencies {

    api "com.android.support:appcompat-v7:$ANDROID_SUPPORT_LIBRARY_VERSION"
    api "com.android.support:recyclerview-v7:$ANDROID_SUPPORT_LIBRARY_VERSION"

    api "org.jetbrains.kotlin:kotlin-stdlib-jre7:$KOTLIN_VERSION"

    testImplementation "junit:junit:$JUNIT_VERSION"
    testImplementation "org.hamcrest:hamcrest-core:$HAMCREST_VERSION"
    testImplementation "org.hamcrest:hamcrest-library:$HAMCREST_VERSION"
    testImplementation "org.hamcrest:hamcrest-integration:$HAMCREST_VERSION"
    testImplementation "com.nhaarman:mockito-kotlin:$MOCKITO_KOTLIN_VERSION"
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$KOTLIN_VERSION"
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib:$KOTLIN_VERSION"
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$KOTLIN_VERSION"
}

dokka {
    outputDirectory = "$rootDir/docs"
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaAndroidTask) {
    delete "$buildDir/docs"

    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/docs"

    externalDocumentationLink {
        url = new URL("https://developer.android.com/reference/")
    }

    // Always recreate documentation
    outputs.upToDateWhen { return false }
}

afterEvaluate {
    if (project.hasProperty("javadocJar")) {
        tasks.javadocJar.dependsOn dokkaJavadoc
    }
}

ext {
    bintrayRepo = 'maven'
    bintrayName = 'material-popup-menu'

    publishedGroupId = 'com.github.zawadz88.materialpopupmenu'
    libraryName = 'Material Popup Menu'
    artifact = 'material-popup-menu'

    libraryDescription = 'Showing Material popup menus programatically made easy'

    siteUrl = 'https://github.com/zawadz88/MaterialPopupMenu'
    gitUrl = 'https://github.com/zawadz88/MaterialPopupMenu.git'

    libraryVersion = '2.1.0'

    developerId = 'zawadz88'
    developerName = 'Piotr Zawadzki'
    developerEmail = 'zawadz88@gmail.com'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}


tasks.withType(Javadoc) {
    excludes = ['**/*.kt']
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {

    // Always recreate documentation
    outputs.upToDateWhen { return false }

    classifier = 'javadoc'
    from new File("$buildDir/docs")
}

publishing {
    publications {
        MyPublication(MavenPublication) {

            artifact sourcesJar
            artifact javadocJar
            artifact("$buildDir/outputs/aar/material-popup-menu-release.aar")

            groupId publishedGroupId
            artifactId artifact

            pom {
                packaging 'aar'
                withXml {

                    asNode().appendNode('name', libraryName)

                    asNode().appendNode('description', libraryDescription)

                    asNode().appendNode('url', siteUrl)

                    // License
                    def licensesNode = asNode().appendNode('licenses')
                    def licenseNode = licensesNode.appendNode('license')
                    licenseNode.appendNode('name', licenseName)
                    licenseNode.appendNode('url', licenseUrl)

                    // Developer
                    def developersNode = asNode().appendNode('developers')
                    def developerNode = developersNode.appendNode('developer')
                    developerNode.appendNode('id', developerId)
                    developerNode.appendNode('name', developerName)
                    developerNode.appendNode('email', developerEmail)

                    // Git
                    def scmNode = asNode().appendNode('scm')
                    scmNode.appendNode('connection', gitUrl)
                    scmNode.appendNode('developerConnection', gitUrl)
                    scmNode.appendNode('url', siteUrl)

                    def dependenciesNode = asNode().appendNode('dependencies')

                    // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                    configurations.implementation.allDependencies.each {
                        // Ensure dependencies such as fileTree are not included.
                        if (it.name != 'unspecified') {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', it.name)
                            dependencyNode.appendNode('version', it.version)
                        }
                    }
                }
            }
        }
    }
}
// Bintray Upload
apply from: '../gradle/tools/bintrayv1.gradle'

publishToMavenLocal.dependsOn 'assembleRelease'